/**
 * 单模块运行管理
 */
project.ext {
    //是否允许module单独调试
    isModuleDebug = false
    moduleName = "libHome"//单独调试module名
    minSdk = 24
    targetSdk = 33
    versionCode = 1
    versionName = "1.0"
    compileSdk = 33


    //动态改变，用于单模块调试
    setAppOrLibDefaultConfig = {
        extension ->
            if (project.ext.isModuleDebug && project.ext.moduleName == project.name) {
                extension.apply plugin: 'com.android.application'
                extension.description "app"
                // 单模块模式下添加theRouter插件
                extension.apply plugin: 'therouter'
            } else {
                extension.apply plugin: 'com.android.library'
                extension.description "lib"
            }
            extension.apply plugin: 'kotlin-android'
            extension.apply plugin: 'kotlin-parcelize'
            extension.apply plugin: 'kotlin-kapt'
            appImplementation = project.name
            //设置通用Android配置
            setAppOrLibAndroidConfig extension.android
    }

    //设置通用的 android配置（可作为project单独调试）
    setAppOrLibAndroidConfig = {
        extension ->
            extension.compileSdk project.ext.compileSdk
            extension.defaultConfig {
                minSdk project.ext.minSdk
                targetSdk project.ext.targetSdk
                versionCode project.ext.versionCode
                versionName project.ext.versionName
                extension.flavorDimensions "versionCode"
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }
            //使用的jdk版本
            extension.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            extension.kotlinOptions {
                jvmTarget = '1.8'
            }
            extension.buildFeatures.viewBinding = true
            extension.buildFeatures.buildConfig = true
            //动态改变清单文件资源指向
            extension.sourceSets {
                main {
                    if (project.ext.isModuleDebug && project.ext.moduleName == project.name) {
                        manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
                    } else {
                        manifest.srcFile 'src/main/AndroidManifest.xml'
                    }
                }
            }
    }
}